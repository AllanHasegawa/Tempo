// Top-level build file where you can add configuration options common to all sub-projects/modules.
ext {
    def versionMajor = 0
    def versionMinor = 3
    def versionPatch = 0
    def versionCode = versionPatch + versionMinor * 100 + versionMajor * 10000

    androidConfig = [
            compileSdkVersion        : 28,
            minSdkVersion            : 16,
            targetSdkVersion         : 28,
            versionCode              : versionCode,
            versionName              : "${versionMajor}.${versionMinor}.${versionPatch}",

            testInstrumentationRunner: "androidx.test.runner.AndroidJUnitRunner"
    ]
}

buildscript {
    ext {
        depPaths = {
            def versions = [
                    gradlePlugin  : '3.3.2',
                    kotlin        : '1.3.21',
                    multidex      : '2.0.1',
                    rxJava        : '2.2.7',
                    rxAndroid     : '2.1.1',
                    androidJob    : '1.1.11',
                    play          : '11.0.2',
                    workManager   : '1.0.0-rc02',
                    junit         : '1.1.0',
                    runner        : '1.2.0',
                    hamkrest      : '1.4.1.0',
                    mockitoKotlin : '1.5.0',
                    mockitoAndroid: '2.8.47',
                    espresso      : '2.2.2',
            ]

            [
                    gradlePlugin    : "com.android.tools.build:gradle:$versions.gradlePlugin",
                    kotlinPlugin    : "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin",
                    multidex        : "androidx.multidex:multidex:$versions.multidex",
                    kotlinStd       : "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin",
                    kotlinReflect   : "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin",
                    supportAppCompat: "androidx.appcompat:appcompat:1.0.0",
                    recyclerView    : "androidx.recyclerview:recyclerview:1.1.0",
//                    supportDesign   : "com.android.support:design:$versions.supportLib",
                    supportCardView : "androidx.cardview:cardview:1.0.0",
                    rxJava          : "io.reactivex.rxjava2:rxjava:$versions.rxJava",
                    rxAndroid       : "io.reactivex.rxjava2:rxandroid:$versions.rxAndroid",
                    androidJob      : "com.evernote:android-job:$versions.androidJob",
                    playGCM         : "com.google.android.gms:play-services-gcm:$versions.play",
                    workManager     : "android.arch.work:work-runtime:$versions.workManager",
                    testRunner      : "androidx.test:runner:$versions.runner",
                    junit           : "androidx.test.ext:junit:$versions.junit",
                    hamkrest        : "com.natpryce:hamkrest:$versions.hamkrest",
                    mockitoKotlin   : "com.nhaarman:mockito-kotlin-kt1.1:$versions.mockitoKotlin",
                    mockitoAndroid  : "org.mockito:mockito-android:$versions.mockitoAndroid",
            ]
        }()
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath depPaths.gradlePlugin
        classpath depPaths.kotlinPlugin

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
